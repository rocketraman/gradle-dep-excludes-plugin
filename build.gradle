buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
  }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
  compile gradleApi()
  compile localGroovy()
}

dependencies {
  testCompile 'junit:junit:4.11'
}

group = 'com.github.rocketraman'
version = '0.1'

jar {
  manifest {
    attributes 'Implementation-Title': 'Gradle Dependency Excludes plugin',
      'Implementation-Version': version,
      'Built-By': System.getProperty('user.name'),
      'Built-Date': new Date(),
      'Built-JDK': System.getProperty('java.version'),
      'Built-Gradle': gradle.gradleVersion
  }
}

def pomConfig = {
  url 'https://github.com/rocketraman/gradle-dep-excludes-plugin'
  inceptionYear '2014'

  scm {
    url 'https://github.com/rocketraman/gradle-dep-excludes-plugin'
    connection 'scm:https://rocketraman@github.com/rocketraman/gradle-dep-excludes-plugin.git'
    developerConnection 'scm:git://github.com/rocketraman/gradle-dep-excludes-plugin.git'
  }

  licenses {
    license {
      name 'The Apache Software License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution 'repo'
    }
  }

  developers {
    developer {
      id 'rocketraman'
      name 'Raman Gupta'
      email 'rocketraman@gmail.com'
    }
  }
}

task sourceJar(type: Jar) {
  group 'Build'
  description 'An archive of the source code'
  classifier 'sources'
  from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'javadoc'
  from groovydoc.destinationDir
}

artifacts {
  sourceJar
  groovydocJar
}

publishing {
  publications {
    mavenPub(MavenPublication) {
      from components.java
      artifact sourceJar
      artifact groovydocJar

      pom.withXml {
        asNode().children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = 'rocketraman'
  key = bintrayApiKey  // specify with -PbintrayApiKey=foo

  publications = ['mavenPub']

  dryRun = false
  publish = true

  pkg {
    repo = 'maven'
    name = 'gradle-dep-excludes-plugin'
    desc = 'Sometimes you just want a simple way to exclude dependencies without creating closures. This plugin creates a method on a Gradle project that provides a little syntax sugar for exclusions.'
    websiteUrl = 'https://github.com/rocketraman/gradle-dep-excludes-plugin'
    issueTrackerUrl = 'https://github.com/rocketraman/gradle-dep-excludes-plugin/issues'
    vcsUrl = 'https://github.com/rocketraman/gradle-dep-excludes-plugin.git'
    licenses = ['Apache-2.0']
    labels = ['gradle']
    publicDownloadNumbers = true
    version {
      name = '0.1'
      released = new Date()
      vcsTag = '0.1'
      attributes = ['gradle-plugin': 'com.github.rocketraman.dep-excludes:com.github.rocketraman:dep-excludes-plugin']
    }
  }
}
